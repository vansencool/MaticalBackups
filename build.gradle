plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.0'
}

group = 'dev.vansen'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation 'org.xerial.snappy:snappy-java:1.1.10.5'
    implementation 'org.apache.commons:commons-compress:1.26.2'
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(targetJavaVersion))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Configure Shadow plugin
shadowJar {
    archiveClassifier.set('') // Remove '-all' from the JAR name
    configurations = [project.configurations.runtimeClasspath] // Include all runtime dependencies
    mergeServiceFiles()
}

tasks.build {
    dependsOn shadowJar
}
